/////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////
//            Deathstalker
/////////////////////////////////////////////////////////////////////
//        Version 1
// Deathstalker, 20th Level Cleric/Mage
//
// 
/////////////////////////////////////////////////////////////////////

/////////////////////////////////////////////////////////////////////
// Prep
/////////////////////////////////////////////////////////////////////
IF
  See([GOODCUTOFF])
  Global("Prep","LOCALS",0)
  CanEquipRanged()
THEN
  RESPONSE #100
    ApplySpell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
    ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // SPWI611.SPL (Protection From Magical Weapons)
    ApplySpell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
    ApplySpell(Myself,WIZARD_HASTE)  // SPWI305.SPL (Haste)
    ApplySpell(Myself,CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Magic)
    SetGlobal("Prep","LOCALS",1)
    SetGlobal("MO_TimeStop","LOCALS",1)
    EquipRanged()
END

IF
  See([GOODCUTOFF])
  Global("Prep","LOCALS",0)
  !CanEquipRanged()
THEN
  RESPONSE #100
    ApplySpell(Myself,WIZARD_SPELL_TURNING)  // SPWI701.SPL (Spell Turning)
    ApplySpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)  // SPWI611.SPL (Protection From Magical Weapons)
    ApplySpell(Myself,WIZARD_STONE_SKIN)  // SPWI408.SPL (Stoneskin)
    ApplySpell(Myself,WIZARD_HASTE)  // SPWI305.SPL (Haste)
    ApplySpell(Myself,CLERIC_RIGHTEOUS_MAGIC)  // SPPR513.SPL (Righteous Magic)
    SetGlobal("Prep","LOCALS",1)
    SetGlobal("MO_TimeStop","LOCALS",1)
END

/////////////////////////////////////////////////////////////////////
// Contingency
/////////////////////////////////////////////////////////////////////
IF
  Global("Prep","LOCALS",1)
  Global("MO_Cont","LOCALS",0)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    DisplayString(Myself,36936)  // ~A contingency spell has been triggered~
    ApplySpell(Myself,WIZARD_IMPROVED_HASTE)
    ApplySpell(Myself,WIZARD_SPELL_IMMUNITY_DIVINATION)
    ApplySpell(Myself,WIZARD_MISLEAD) 
    SetGlobal("MO_Cont","LOCALS",1)
    SetInterrupt(TRUE)
  RESPONSE #100
    SetInterrupt(FALSE)
    DisplayString(Myself,36936)  // ~A contingency spell has been triggered~
    ReallyForceSpell(NearestEnemyOf(Myself),CLERIC_INSECT_PLAGUE)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_GREATER_MALISON)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_SPHERE_OF_CHAOS)
    SetGlobal("MO_Cont","LOCALS",1)
    SetInterrupt(TRUE)
  RESPONSE #100
    SetInterrupt(FALSE)
    DisplayString(Myself,36936)  // ~A contingency spell has been triggered~
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_CHAOS)
    ReallyForceSpell(NearestEnemyOf(Myself),WIZARD_DEATH_SPELL)
    SetGlobal("MO_Cont","LOCALS",1)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Spell: Mantle
/////////////////////////////////////////////////////////////////////
IF
  OR(2)
    !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
    CheckStatGT(Myself,0,AURACLEANSING)
  See(NearestEnemyOfType([GOODCUTOFF]))
  Range(LastSeenBy(Myself),4)
  HaveSpell(WIZARD_MANTLE)
  !CheckSpellState(Myself,PROTECTION_FROM_MAGICAL_WEAPONS)
  !CheckSpellState(Myself,ABSOLUTE_IMMUNITY)
  !CheckSpellState(Myself,MANTLE)
  !CheckSpellState(Myself,IMPROVED_MANTLE)
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetInterrupt(FALSE)
    Spell(Myself,WIZARD_MANTLE)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Heal!
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  HasItemEquiped("POTN55",Myself)
  HPPercentLT(Myself,60)
THEN
  RESPONSE #100
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    UseItem("POTN55",Myself)  // ~Potion of Superior Healing~
END

/////////////////////////////////////////////////////////////////////
// Spell: Timestop
/////////////////////////////////////////////////////////////////////
IF
  !GlobalTimerNotExpired("MO_SpellCastTimer","LOCALS")
  !GlobalTimerNotExpired("MO_TimeStopTimer","LOCALS")
  GlobalGT("MO_TimeStop","LOCALS",0)
  Global("MO_Cont","LOCALS",1) 
  See(NearestEnemyOfType([GOODCUTOFF]))
  CheckStatLT(Myself,20,SPELLFAILUREMAGE)
  !CheckStatGT(Myself,0,CLERIC_INSECT_PLAGUE)
  !StateCheck(Myself,STATE_POISONED)
  !StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    IncrementGlobal("MO_TimeStop","LOCALS",-1)
    SetGlobalTimer("MO_SpellCastTimer","LOCALS",6)
    SetGlobalTimer("MO_TimeStopTimer","LOCALS",30)
    SetInterrupt(FALSE)
    ForceSpell(Myself,WIZARD_TIME_STOP)
    SetInterrupt(TRUE)
END

/////////////////////////////////////////////////////////////////////
// Enemy Distance Check
/////////////////////////////////////////////////////////////////////
IF 
  Range(NearestEnemyOf(Myself),5)
  IsWeaponRanged(Myself)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    Continue()
END

IF 
  !Range(NearestEnemyOf(Myself),5)
  !IsWeaponRanged(Myself)
  CanEquipRanged()
THEN
  RESPONSE #100
    EquipRanged()
    Continue()
END

/////////////////////////////////////////////////////////////////////
// Move if damaged and enemy is close
/////////////////////////////////////////////////////////////////////
/*
IF
  HPPercentLT(Myself,50)
  Range(NearestEnemyOf(Myself),5)
  See(NearestEnemyOf(Myself))
THEN
  RESPONSE #100
    RunAwayFrom(NearestEnemyOf(Myself),15) // One second running away before re-evaluate
END
*/

/////////////////////////////////////////////////////////////////////
// Attack
/////////////////////////////////////////////////////////////////////
IF
  ActionListEmpty()
  See(MostDamagedOf([PC]))
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  See(WorstAC([PC]))
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(LastSeenBy(Myself),MAINHAND)
  WeaponCanDamage(LastSeenBy(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(LastSeenBy(Myself))
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),1,STONESKINS)
  CheckStatGT(LastSeenBy(Myself),-20,ARMORCLASS)
  CheckStatLT(LastSeenBy(Myself),60,RESISTCRUSHING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTPIERCING)
  CheckStatLT(LastSeenBy(Myself),60,RESISTMISSILE)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(NearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(NearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(NearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SecondNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SecondNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SecondNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SecondNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(ThirdNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(ThirdNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(ThirdNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(ThirdNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FourthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FourthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FourthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FourthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(FifthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(FifthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(FifthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(FifthNearestEnemyOf(Myself))
END

IF
  ActionListEmpty()
  See(SixthNearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !StateCheck(LastSeenBy(Myself),STATE_NOT_APPROACHABLE)
  WeaponEffectiveVs(SixthNearestEnemyOf(Myself),MAINHAND)
  WeaponCanDamage(SixthNearestEnemyOf(Myself),MAINHAND)
THEN
  RESPONSE #100
    AttackOneRound(SixthNearestEnemyOf(Myself))
END

/////////////////////////////////////////////////////////////////////
// Find them!
/////////////////////////////////////////////////////////////////////
IF
  Heard([ENEMY],125)
  Allegiance(Myself,ENEMY)
  !Detect([GOODCUTOFF])
THEN
  RESPONSE #100
    MoveToObject(LastHeardBy(Myself))
END
